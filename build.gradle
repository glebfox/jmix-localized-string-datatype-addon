/*
 * Copyright 2024 Gleb Gorelov.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.jmix.gradle:jmix-gradle-plugin:2.3.0"
    }
}

group = 'com.glebfox.jmix.locstr'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url 'https://global.repo.jmix.io/repository/public'
        }
    }

    jmix {
        bomVersion = '2.3.0'
        projectId = 'locstr'
    }

    group = this.group
    version = this.version

    if (it.name != 'jmix-localized-string-datatype-demo') {
        def props = new Properties()
        buildFile.withInputStream { props.load(it) }
        def subArchivesBaseName = props.getProperty('archivesBaseName')
        def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)

        java {
            withSourcesJar()
        }

        artifacts {
            archives sourcesJar
        }

        publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
                    credentials {
                        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                        password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                    }
                }
            }
            publications {
                javaMaven(MavenPublication) {
                    artifactId = archName
                    from components.java
                }
            }
        }

        // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
        tasks.withType(GenerateModuleMetadata) {
            enabled = false
        }
    }
}
